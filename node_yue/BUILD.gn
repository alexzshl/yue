# Copyright 2017 Cheng Zhao. All rights reserved.
# Use of this source code is governed by the license that can be found in the
# LICENSE file.

import("//v8binding/node.gni")

loadable_module("node_yue") {
  output_name = "gui"
  output_extension = "node"
  output_prefix_override = true  # do not add "lib" prefix

  sources = [
    "binding_gui.cc",
    "binding_signal.h",
    "binding_values.cc",
    "binding_values.h",
    "node_integration.cc",
    "node_integration.h",
    "v8_value_converter.h",
    "v8_value_converter_impl.cc",
    "v8_value_converter_impl.h",
  ]

  if (is_linux) {
    sources += [
      "node_integration_linux.cc",
      "node_integration_linux.h",
    ]
  }

  if (is_mac) {
    sources += [
      "chrome_view_mac.mm",
      "chrome_view_mac.h",
      "node_integration_mac.cc",
      "node_integration_mac.h",
    ]
  }

  if (is_win) {
    sources += [
      "delay_load_hook_win.cc",
      "node_integration_win.cc",
      "node_integration_win.h",
    ]
  }

  deps = [
    "//base",
    "//nativeui",
    "//v8binding",
  ]

  if (is_linux && is_component_build) {
    configs += [ "//build/config/gcc:rpath_for_built_shared_libraries" ]
  }

  if (is_win) {
    ldflags = [
      "/DELAYLOAD:node.exe",
      "/DELAYLOAD:node.dll",
      "/DELAYLOAD:yode.exe",
    ]
  }
}
